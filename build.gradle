buildscript {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    dependencies {
        classpath 'com.palantir.jakartapackagealignment:jakarta-package-alignment:0.6.0'
        classpath 'com.palantir.gradle.jdks:gradle-jdks:0.44.0'
        classpath 'com.palantir.gradle.jdkslatest:gradle-jdks-latest:0.14.0'
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.15.0'
        classpath 'com.palantir.gradle.failure-reports:gradle-failure-reports:1.10.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.49.0'
        classpath 'com.gradle.publish:plugin-publish-plugin:1.2.1'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.23.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:5.61.0'
        classpath 'com.palantir.gradle.conjure:gradle-conjure:5.50.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:3.1.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.7.0'
    }
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.failure-reports'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.baseline-java-versions'
apply plugin: 'com.palantir.jdks.latest'

version System.env.CIRCLE_TAG ?: gitVersion()

allprojects {
    apply plugin: 'com.palantir.java-format'
    apply plugin: 'com.palantir.jakarta-package-alignment'
    version rootProject.version
    group 'com.palantir.metricschema'

    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'
    apply plugin: 'com.palantir.baseline-class-uniqueness'

    tasks.withType(JavaCompile) {
        options.compilerArgs += [
            '-Werror',
            '-Xlint:deprecation',
            '-Xlint:unchecked',
        ]
    }

    tasks.check.dependsOn(javadoc)

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly("org.junit.vintage:junit-vintage-engine") {
            because 'allows JUnit 3 and JUnit 4 tests to run'
        }
    }
}

javaVersions {
    libraryTarget = 11
    runtime = 17
}
