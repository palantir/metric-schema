buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:0.3.18'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.10.1'
        classpath 'com.netflix.nebula:gradle-info-plugin:7.1.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:17.0.5'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.17.3'
        classpath 'com.palantir.baseline:gradle-baseline-java:3.5.0'
        classpath 'com.palantir.gradle.conjure:gradle-conjure:4.19.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.2'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.3.0'
    }
}

apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'

version System.env.CIRCLE_TAG ?: gitVersion()

allprojects {
    apply plugin: 'com.palantir.java-format'
    version rootProject.version
    group 'com.palantir.metricschema'

    repositories {
        jcenter()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'
    apply plugin: 'com.palantir.baseline-class-uniqueness'

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Werror']
    }

    tasks.check.dependsOn(javadoc)
    sourceCompatibility = 1.8

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly("org.junit.vintage:junit-vintage-engine") {
            because 'allows JUnit 3 and JUnit 4 tests to run'
        }
    }
}
